// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.

    // X pre-processing
    Mux16(a=x, b=false, sel=zx, out=outzerox); 
    Not16(in=outzerox, out=notoutzerox); 
    Mux16(a=outzerox, b=notoutzerox, sel=nx, out=prex);

    // Y pre-processing
    Mux16(a=y, b=false, sel=zy, out=outzeroy); 
    Not16(in=outzeroy, out=notoutzeroy); 
    Mux16(a=outzeroy, b=notoutzeroy, sel=ny, out=prey);

    // Function Selector
    And16(a=prex, b=prey, out=xandy);
    Add16(a=prex, b=prey, out=xplusy);
    Mux16(a=xandy, b=xplusy, sel=f, out=outfunction); 

    // Negating output
    Not16(in=outfunction, out=notoutfunction);
    Mux16(a=outfunction, b=notoutfunction, sel=no, out=outdummy); 

    // Dummy output for the zero and negative detector
    Not16(in=outdummy, out=notoutdummy);
    Not16(in=notoutdummy, out=out);

    // Zero detector
    Not16(in=notoutdummy, out[0..7]=lowout, out[8..15]=highout);
    Or8Way(in=lowout, out=outzr1); 
    Or8Way(in=highout, out=outzr2); 
    Or(a=outzr1, b=outzr2, out=notzr);
    Not(in=notzr, out=zr); 

    // Negative detector
    Not16(in=outdummy, out[0..14]=dummyout, out[15]=outng1);
    Not(in=outng1, out=ng);
}